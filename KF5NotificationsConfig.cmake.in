@PACKAGE_INIT@

set(KNOTIFICATIONS_DBUS_INTERFACES_DIR "@PACKAGE_KDE_INSTALL_DBUSINTERFACEDIR@")

include(CMakeFindDependencyMacro)
find_dependency(Qt@QT_MAJOR_VERSION@Gui @REQUIRED_QT_VERSION@)
if (@HAVE_WIDGETS@)
    find_dependency(Qt@QT_MAJOR_VERSION@Widgets @REQUIRED_QT_VERSION@)
endif()
if (@HAVE_DBUS@)
    find_dependency(Qt@QT_MAJOR_VERSION@DBus @REQUIRED_QT_VERSION@)
endif()

if (NOT @BUILD_SHARED_LIBS@) 
    find_dependency(KF5CoreAddons "@KF_DEP_VERSION@")
    find_dependency(KF5Config "@KF_DEP_VERSION@")

    if (@HAVE_KWINDOWSYSTEM@)
        find_dependency(KF5WindowSystem "@KF_DEP_VERSION@")
    endif()

    if (@WITH_SNORETOAST@)
        find_dependency(LibSnoreToast)
        find_dependency(Qt@QT_MAJOR_VERSION@Network @REQUIRED_QT_VERSION@)
    endif()

    if (@HAVE_PHONON4QT5@)
        find_dependency(Phonon4Qt5)
    endif()

    if (@Canberra_FOUND@)
        find_dependency(Canberra)
    endif()

    if (@Qt5TextToSpeech_FOUND@)
        find_dependency(Qt5TextToSpeech @REQUIRED_QT_VERSION@)
    endif()

    if(@HAVE_X11@)
        if (NOT TARGET Qt6::Gui)
            find_dependency(Qt5X11Extras @REQUIRED_QT_VERSION@)
        endif()
    endif()

    if(APPLE)
        find_dependency(Qt5MacExtras @REQUIRED_QT_VERSION@)
    endif()

    if(@HAVE_DBUSMENUQT@)
        find_dependency(dbusmenu-qt5)
    endif()

    if (ANDROID)
        find_dependency(Qt5AndroidExtras @REQUIRED_QT_VERSION@)
    endif()
endif()

include("${CMAKE_CURRENT_LIST_DIR}/KF5NotificationsTargets.cmake")
@PACKAGE_INCLUDE_QCHTARGETS@
